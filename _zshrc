source /usr/local/etc/bash_completion.d/git-prompt.sh

# options
setopt AUTO_CD
setopt CORRECT
setopt PROMPT_SUBST

# completion
#if type brew &>/dev/null ; then
#  # https://docs.brew.sh/Shell-Completion
#  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
#fi
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
autoload -Uz compinit && compinit

# prompt
_diogo_set_prompt () {
  # newline, delimiters
  local N=$'\n' SL='%S▌' SEP='│' SR='▐%s'
  # italic, reset
  local I=$'\e[3m' R=$'\e[0m'
  # bright colors
  local BK='8' BR='9' BG='10' BY='11' BB='12' BM='13' BC='14' BW='15'

  # git
  local GL=" %F{$BM}$SL" GR="$SR%f"
  local G="\$(__git_ps1 '${GL//\%/%%}%s${GR//\%/%%}')"
  GIT_PS1_STATESEPARATOR="$SEP"
  GIT_PS1_SHOWDIRTYSTATE=1      # *: unstaged, +: staged changes
  GIT_PS1_SHOWSTASHSTATE=1      # $: stash is not empty
  GIT_PS1_SHOWUNTRACKEDFILES=1  # %: untracked files
  GIT_PS1_SHOWUPSTREAM='auto'   # <: behind, >: ahead, <>: diverged, =: same

  local X="%(?..%F{r}$SL%?$SR%f)"   # non-zero exit
  local F="%(!.%F{r}.%F{c})"        # main fg color

  PROMPT="$X$N$F╭ $I%1~$R%f$G$N$F╰─>%f "
  RPROMPT="%F{$BK}%T%f"
  PROMPT2="%F{c}%_…%f "

  DIOGO_RPROMPT="$RPROMPT"
  DIOGO_VICMD="%F{y}--NORMAL--%f"
} && _diogo_set_prompt

# vi keymap
bindkey -v
KEYTIMEOUT=1
function zle-line-init zle-keymap-select {
  # https://web.archive.org/web/*/https://dougblack.io/words/zsh-vi-mode.html
  RPROMPT="${${KEYMAP/vicmd/$DIOGO_VICMD}/(main|viins)/$DIOGO_RPROMPT}"
  zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select
bindkey '^?' backward-delete-char # allow backspace on return from normal mode
bindkey '^r' history-incremental-search-backward

# aliases
alias ls='ls -G'
alias l='ls -A'
alias ll='ls -lhA'
alias grep='grep --color=auto'

alias rc='mvim ~/.zshrc'
alias sha1='shasum -a 1 -b'
alias sha256='shasum -a 256 -b'

alias ava='nocorrect ava'
alias jest='nocorrect jest'

alias nohist='unset HISTFILE'

# --------

# git
alias g='git'
alias gs='git status --short --branch --show-stash'

ga () { git add "$@" && gs ; }
alias ga.='ga .'

alias gd='git diff'
alias gdw='git diff --word-diff=color --word-diff-regex=.'
alias gdc='git diff --cached'

alias gl='git log --graph --oneline'

gci () {
  git commit --verbose "$@" && echo &&
  gs &&
  osascript -e 'tell application "Terminal" to activate'
}

# Homebrew
brew-up () {
  brew update   && echo &&
  #brew outdated && echo &&
  brew upgrade  && echo &&
  brew cleanup
}

alias brew-why='brew uses --installed'

# Node.js & npm
alias node-nohist='NODE_REPL_HISTORY="" node'
export PATH="${PATH}:./node_modules/.bin"

# nvm
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm

nvm-up () {   # https://github.com/nvm-sh/nvm#manual-upgrade
  (
    cd "$NVM_DIR"
    git fetch --tags origin
    git checkout `git describe --abbrev=0 --tags --match "v[0-9]*" $(git rev-list --tags --max-count=1)`
  ) && \. "$NVM_DIR/nvm.sh"
}

# Python
alias py='python3'
export PYTHONSTARTUP="${HOME}/.pythonstartup"

## Pipenv
alias px='pipenv run'
#export PATH="${PATH}:./.venv/bin"
export PIPENV_VENV_IN_PROJECT=1

# pyenv
#eval "$(pyenv init -)"

# rbenv
eval "$(rbenv init - zsh)"

# Redocly
#alias redoc='redoc-cli serve docs/swagger.y*ml --watch'
alias redoc='redocly preview-docs docs/swagger.y*ml'
alias redoc-build='redoc-cli build docs/swagger.y*ml'

# Rust
#. "$HOME/.cargo/env"
export PATH="${PATH}:${HOME}/.cargo/bin"

# SourceTree
#alias stree='open -a /Applications/SourceTree.app'

# Vim
export PATH="${PATH}:/Applications/MacVim.app/Contents/bin"
alias vim-up='~/Code/dotfiles/scripts/install-vim-plugins'
export GIT_EDITOR='mvim -f'

# ~/.bin
#export PATH="${PATH}:${HOME}/.bin"

# --------

abspath () {
  python3 -c 'import os.path, sys; print(os.path.abspath(sys.argv[1]))' "$1"
}

argtest () {
  python3 -c 'import sys; print(sys.argv[1:])' "$@"
}

cdf () {
  # Change directory to the current folder of the topmost Finder window
  cd "$(pfd)"
}

ding () {
  echo -n "\a"
}

gitio () {
  # Create a custom short URL on git.io
  if [[ "$#" = 2 ]] ; then
    curl -i 'https://git.io' -F "url=$2" -F "code=$1"
  else
    echo 'usage: gitio [ALIAS] [GITHUB_URL]' 1>&2
    return 1
  fi
}

hilite () {
  grep --color -E -- "$1|\$" "${@:2}"
}

jqx () {
  jq -Rr '. as $line | try fromjson catch $line'
}

kill-by-port () {
  lsof -i:"$1" && sudo kill $(lsof -t -i:"$1")
}

pfd () {
  # Print the path to the current folder of the topmost Finder window
  osascript -e '
    tell application "Finder"
      return POSIX path of (insertion location as alias)
    end tell
  '
}

pman () {
  # View man page in Preview.app
  #man -t "$@" | open -f -a Preview
  mandoc -T pdf "$(/usr/bin/man -w "$@")" | open -fa Preview
}

ql () {
  # Show file contents via Quick Look
  (( $# > 0 )) && qlmanage -p "$@" &>/dev/null
}

que () {
  local result; result="$(whence -vaS "$1")"
  local exit_status=$?
  echo "$result"
  [[ "$result" =~ 'shell function' ]] && whence -fx2 "$1"
  return $exit_status
}

yd () {
  if [[ "$1" = '-l' ]] ; then
    local options=(--format '[height <=? 720]') && shift
  else
    local options=(--format '[height <=? 1080]')
  fi
  ( cd ~/Downloads && youtube-dl "${options[@]}" "$@" )
}

# --------

[[ -f ~/Work/_zshrc ]] && source ~/Work/_zshrc
true

# vim: ft=zsh
